<RE>            ::= <RE> "|" <simple-RE> | <simple-RE>
<simple-RE>     ::= <simple-RE> <basic-RE> | <basic-RE>
<basic-RE>      ::= <star> | <plus> | <elementary-RE>
<star>          ::= <elementary-RE> "*"
<plus>          ::= <elementary-RE> "+"
<elementary-RE> ::= <group> | <any> | <char> | <set>
<group>         ::= "(" <RE> ")"
<any>           ::= "."
<char>          ::= any non metacharacter | "\" metacharacter
<set>           ::= <positive-set> | <negative-set>
<positive-set>  ::= "[" <set-items> "]"
<negative-set>  ::= "[^" <set-items> "]"
<set-items>     ::= <set-item> | <set-items> <set-item>
<set-item>      ::= <range> | <char>
<range>         ::= <char> "-" <char>
